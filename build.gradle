plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'

	// https://github.com/Juuxel/loom-quiltflower-mini
	id 'io.github.juuxel.loom-quiltflower-mini' version '1.2.1'
}

repositories {
	maven {
		url 'https://jitpack.io'
	}
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

String versionSuffix = ''
if (System.getenv("BUILD_RELEASE") != "true") {
	versionSuffix += '-SHAPSHOT'
}
String buildNumber = System.getenv("BUILD_ID")
if (buildNumber != null) {
	versionSuffix += '+build.' + buildNumber
}
String fullModVersion = project.mod_version + versionSuffix

archivesBaseName = project.archives_base_name
version = 'mc' + project.minecraft_version + '-v' + fullModVersion
group = project.maven_group

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// runtime mods
	modRuntimeOnly "com.github.astei:lazydfu:0.1.2"  // faster startup

	modImplementation fabricApi.module("fabric-resource-loader-v0", project.fabric_api_version)
	include fabricApi.module("fabric-resource-loader-v0", project.fabric_api_version)
}

processResources {
	inputs.property "version", fullModVersion

	filesMatching("fabric.mod.json") {
		expand "version": fullModVersion
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
